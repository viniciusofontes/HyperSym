! GetResults ---------------------------------------------------------------
! Article: Fontes, V.O., Leit√£o, A.X., & Pereira, A. (2023). 
!          HyperSym: a Matlab code for symbolic differentiation of 
!          ready-to-use tensors in hyperelastic finite element analysis, 
!          Computer Applications in Engineering Education.
!          DOI: https://doi.org/xx.xxxx/xxxxxx-xxx-xxxx-x
!
! References:
! [1] Kim, N.-H. (2014). Introduction to Nonlinear Finite Element Analysis.
!     Springer Science & Business Media, pp. 214-216.
! [2] ANSYS, Inc. (2021). Ansys Mechanical APDL Verification Manual, 
!     pp. 157-158. Example VM56: Hyperelastic Thick Cylinder Under Internal
!     Pressure.
! [3] Oden, J.T. (1972). Finite Elements of Nonlinear Continua, Courier 
!     Corporation, pp. 325-331.
! -------------------------------------------------------------------------

/POST1

/DSCALE,1,1        ! scale factor
PLDISP,2           ! plots deformed shape

/DIST,1,1.4,1      ! adjusts zoom
/REPLOT,FAST

SET,LAST

ETABLE,SX,S,X
AVPRIN,0,0

el = 0
dx = (ro-ri)/Nex

NSEL,S,LOC,X,ri-dx/2,ri+dx/2
ESLN
el = ELNEXT(el)    ! element # 1

*GET,TX,ELEM,el,ETABLE,SX
ESEL,ALL
NSEL,ALL

n = node(ri,0,0)
*GET,Disp,NODE,n,U,X

! Preallocation
*DIM,LABEL,CHAR,2,2
*DIM,VALUE,ARRAY,2,3

LABEL(1,1) = 'Disp.'
LABEL(2,1) = 'Stress'

VALUE(1,1) = 7.1800*pol2mm
VALUE(1,2) = Disp
VALUE(1,3) = ABS(Disp/7.180)
VALUE(2,1) = -122.0*psi2MPa
VALUE(2,2) = TX
VALUE(2,3) = ABS(TX/(-122.0*psi2MPa))

*CFOPEN,'Results_%MatModel%','txt'
*VWRITE,'Radial response at el # 1:'
%26S
*VWRITE,' '
%S
*VWRITE,' ','Ref. Value','Ansys Value','Ratio of Error'
%6S %10S %11S %14S
*VWRITE,LABEL(1,1),VALUE(1,1),VALUE(1,2),VALUE(1,3)
%6S %10.4F %11.4F %14.4F
*CFCLOSE