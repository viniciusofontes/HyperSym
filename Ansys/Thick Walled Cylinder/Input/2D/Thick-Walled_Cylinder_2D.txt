! Thick-Walled Cylinder_2D ------------------------------------------------
! Article: Fontes, V.O., Leit√£o, A.X., & Pereira, A. (2023). 
!          HyperSym: a Matlab code for symbolic differentiation of 
!          ready-to-use tensors in hyperelastic finite element analysis, 
!          Computer Applications in Engineering Education.
!          DOI: https://doi.org/xx.xxxx/xxxxxx-xxx-xxxx-x
!
! References:
! [1] Kim, N.-H. (2014). Introduction to Nonlinear Finite Element Analysis.
!     Springer Science & Business Media, pp. 214-216.
! [2] ANSYS, Inc. (2021). Ansys Mechanical APDL Verification Manual, 
!     pp. 157-158. Example VM56: Hyperelastic Thick Cylinder Under Internal
!     Pressure.
! [3] Oden, J.T. (1972). Finite Elements of Nonlinear Continua, Courier 
!     Corporation, pp. 325-331.
! -------------------------------------------------------------------------

FINISH
/CLEAR
/TITLE, Thick-Walled Cylinder under Internal Pressure

/VIEW,1,1,0,1
/REP,FAST   
/REPLOT,RESIZE  
/PLOPTS,DATE,OFF   ! removes the time and date in the upper right corner

! Changing background colors:
/RGB,INDEX,100,100,100,0
/RGB,INDEX,80,80,80,13
/RGB,INDEX,60,60,60,14
/RGB,INDEX,0,0,0,15

!-----------------------------------------------------------------------------------
! PREPROCESSING
!-----------------------------------------------------------------------------------

/PREP7
/TRIAD,OFF         ! hides global coordinate system

! Initial Parameters:
pol2mm = 25.4
ri = 7*pol2mm      ! inner radious (mm)
ro = 18.625*pol2mm ! outer radious (mm)
h = 0.775*pol2mm   ! "height" (mm)

Nex = 20           ! number of elements in radial direction
Ney = 20           ! number of elements in tangential direction
Nez = 1            ! number of elements in axial direction

psi2MPa = 6.8957572932E-3
p = 150*psi2MPa    ! pressure (MPa)

! Define Material Model:
!MatModel = 0      ! = Linear
!MatModel = 1      ! = Mooney-Rivlin
!MatModel = 2      ! = Yeoh
!MatModel = 3      ! = Neo-Hookean

*ASK,MatModel,'THE MATERIAL MODEL: ',1 

! Material Selection:
*IF,MatModel,EQ,0,THEN
	MatModel = 'Linear'
*ELSEIF,MatModel,EQ,1,THEN
	MatModel = 'Mooney-Rivlin'
*ELSEIF,MatModel,EQ,2,THEN
	MatModel = 'Yeoh'
*ELSEIF,MatModel,EQ,3,THEN
	MatModel = 'Neo-Hookean'
*ENDIF

/INPUT,MatModel,'txt',../Input/Materials

! Element:
ET,1,PLANE182
KEYOPT,1,3,2       ! plain strain

! Area and Volume
CYL4,0,0,ri,90,ro

! Mesh creation:
! Horizontal and vertical lines
LSEL,S,LOC,X,0
LSEL,A,LOC,Y,0
LESIZE,ALL,,,Nex,,1     ! defines "refinement" in a specific line

! Circular lines
ALLSEL
LSLA,S             ! selects those lines contained in the selected areas
LSEL,U,LOC,X,0
LSEL,U,LOC,Y,0
LESIZE,ALL,,,Ney,,1

MSHAPE,0,2D        ! specifies the element shape to be used for meshing
MSHKEY,1           ! specifies whether use free or mapped mesh (1 = mapped)
ASEL,,,,ALL
AMESH,ALL          ! meshes all the areas

! Boundary Conditions:
! Prescribed displacements
NSEL,S,LOC,X,0
D,ALL,UX,0
NSEL,S,LOC,Y,0
D,ALL,UY,0

CSYS,1   ! cylindrical coordinate system with global Cartesian Z set as the rotation axis
NROTAT,ALL         ! rotating nodes to cylindrical coordinates

!-----------------------------------------------------------------------------------
! SOLUCTION
!-----------------------------------------------------------------------------------

/SOLU

! Applied pressure
LSEL,S,LOC,X,ri
NSLL                 ! selects nodes from lines
SFL,ALL,PRES,p       ! internal pressure

ANTYPE,STATIC           ! static analysis
NLGEOM,ON               ! sets nonlinear displacements
NSUBST,10,100,10,OFF    ! defines the number of substeps
OUTRES,ALL,ALL          ! controls the solution data written to the database
OUTPR,BASIC,ALL         ! reads the load case file
/OUTPUT,,'inp'          ! redirects text output to a file or to the screen
LSWRITE                 ! writes load step file: Jobname.S01

SOLVE

!-----------------------------------------------------------------------------------
! POSTPROCESSING
!-----------------------------------------------------------------------------------

/POST1

ERESX,YES               ! specifies extrapolation of integration point results

RSYS,1
PLDISP,2                ! plots deformed shape

/DIST,1,1.4,1      ! adjusts zoom
/AUTO,1
/REPLOT,FAST